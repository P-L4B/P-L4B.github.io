1  REM  BY JADER MONARI - PORTED TO APPLESOFT BASIC BY F. SBLENDORIO 2024
2  PRINT "INITIALIZING..."
3 S$ = "   ":T$ = "     ":P$ = "": FOR I = 1 TO 255
4  IF I > 64 THEN P$ = P$ + "*": GOTO 7
5  IF I > 47 AND I < 58 THEN P$ = P$ + "@": GOTO 7
6 P$ = P$ + " "
7  NEXT 
8  DIM A$(21): DIM F$(21): DIM BK$(21): DIM V$(3,21): DIM MEM(6)
10  CLS 
12  GOSUB 7000
15  INPUT "GAME TYPE 1, 2 OR 3 ?";G:G = G - 1: PRINT 
20  INPUT "FAST LEARNING (Y/N)?";Q$: PRINT 
30  INPUT "WHAT'S YOUR NAME ?";N$
40  PRINT "PLEASE WAIT..."
50  GOSUB 500: REM   STATE LOADER
60  GOSUB 600: REM   WINNINGS LOADER
90 S = 0:V = 0:A = 0:SK = 0:SC$ = A$(0)
93  GOSUB 5000
95  PRINT : PRINT "PCM PLAYS THE FIRST MOVE"
96  PRINT "PRESS ENTER TO SHOW IT";: INPUT "";Z$: PRINT 
100  GOSUB 700: REM PCM PLAYS
105  GOSUB 900: REM UPDATE STATUS
110  GOSUB 5000
140 A = 1: PRINT:PRINT "NOW ";N$;" PLAYS THE FIRST MOVE"
150  GOSUB 760
155  GOSUB 900: REM AGGIORNA STATO CORRENTE
160  FOR Y = 0 TO 20: REM   AGGIORNA PUNTATORE STATO
170  IF SC$ = A$(Y) THEN S = Y: GOTO 200
180  NEXT Y
200  GOSUB 5000
230 A = 0: PRINT: PRINT "PCM PLAYS THE SECOND MOVE"
232  PRINT "PRESS ENTER TO SHOW IT";: INPUT "";Z$: PRINT 
235 SK = 2: REM   UPDATE STACK POINTER
240  GOSUB 700
245  GOSUB 900: REM UPDATE STATE MACHINE
250  GOSUB 5000
300 A = 1: PRINT : PRINT "NOW ";N$;" PLAYS THE SECOND MOVE"
310  GOSUB 760
315  GOSUB 900: REM   UPDATE STATE MACHINE
320  GOSUB 5000
380  REM   CHECK WIN
385 SK = 0: REM   ZERO STACK POINTER
390  IF G = 0 THEN N = 2: GOTO 420
400  IF G = 1 THEN N = 8: GOTO 420
410 N = 17
420  FOR I = 0 TO N
425  IF SC$ = V$(G,I) THEN V = 1: GOTO 440
430  NEXT I
440  IF V = 1 THEN 480
450  PRINT : PRINT "PCM WINS!": REM   ADD WINNING TRANSACTIONS
455  PRINT "---PRESS ENTER FOR ANOTHER MATCH"
460 S = MEM(SK):C = MEM(SK + 1):C$ =  RIGHT$ ( STR$ (C),1):F$(S) = F$(S) + C$
470 S = MEM(SK + 2):C = MEM(SK + 3):C$ =  RIGHT$ ( STR$ (C),1):F$(S) = F$(S) + C$
475  INPUT "";Z$: GOTO 90
480  PRINT : PRINT N$;" WINS!": GOSUB 945: REM   REMOVE LOSING TRANSACTIONS
485  PRINT "---PRESS ENTER FOR ANOTHER MATCH";
490  INPUT "";Z$: GOTO 90
500  REM   STATE LOADER
510  FOR Y = 0 TO 20
520  FOR I = 1 TO 6: READ B$:A$(Y) = A$(Y) + B$:
530  IF  ASC (B$) = 45 THEN F$(Y) = F$(Y) +  RIGHT$ ( STR$ (I),1):BK$(Y) = F$(Y)
540  NEXT I: NEXT Y
550  RETURN 
600  REM   WINNINGS LOADER
605 N = 2
610  FOR Y = 0 TO 2
620  FOR I = 0 TO N: FOR A = 0 TO 5: READ B$:V$(Y,I) = V$(Y,I) + B$: NEXT A
625  NEXT I
630  IF I = 3 THEN N = 8: GOTO 660
640  IF I = 9 THEN N = 17: GOTO 660
650  IF I = 18 THEN  RETURN 
660  NEXT Y
700  REM   PCM PLAYS
720 N =  INT ( RND (1) *  LEN (F$(S))) + 1:C$ =  MID$ (F$(S),N,1)
735  GOSUB 800: REM   CHECK IF THE SAME NUMBER EXISTS IN THE SAME POSITION
740  IF F = 1 THEN 720: REM   PICK ANOTHER ONE
745 MEM(SK) = S:MEM(SK + 1) =  VAL (C$): REM  SAVE STATE/CHAR IN THE STACK 
750  RETURN 
760  REM   HUMAN PLAYS
765  INPUT "YOUR TOKEN'S POSITION?";C$
766  IF C$ = "" THEN 765
770  GOSUB 850
775  IF F = 1 THEN  PRINT : PRINT "INVALID INPUT... TRY AGAIN": GOTO 765
780  GOSUB 800: REM   CHECK IF THE SAME NUMBER EXISTS IN THE SAME POSITION
785  IF F = 1 THEN  PRINT : PRINT "INVALID INPUT... TRY AGAIN": GOTO 765
790  RETURN 
800  REM   CHECK IF THE SAME NUMBER EXISTS IN THE SAME POSITION
805  IF A = 1 THEN C =  ASC (C$) - 64: GOTO 810: REM   ALICE'S NUMBER POSITION
807  IF A = 0 THEN C =  VAL (C$): GOTO 810: REM   BOB'S NUMBER POSITION
810 B$ =  MID$ (SC$,C,1)
820  IF  ASC (B$) <  > 45 THEN F = 1: RETURN 
830 F = 0: RETURN 
850  REM   CHECK CHARACTER INSERTION
860  IF ( ASC (C$) < 66 OR  ASC (C$) > 71) THEN F = 1: RETURN 
870 F = 0: RETURN 
900  REM   UPDATE CURRENT STATUS
905  IF A = 1 THEN C =  ASC (C$) - 64: GOTO 910: REM   ALICE'S NUMBER POSITION
907  IF A = 0 THEN C =  VAL (C$): GOTO 910: REM   BOB'S NUMBER POSITION
910 L$ = "": IF C > 1 THEN L$ =  LEFT$ (SC$,C - 1)
920 R$ = "": IF C < 6 THEN R$ =  RIGHT$ (SC$,6 - C)
930 SC$ = L$ + C$ + R$
940  RETURN 
945  REM   DELETE STATES
947  IF Q$ = "N" THEN 996
950 SK = 0
955 S = MEM(SK):B = MEM(SK + 1):B$ =  RIGHT$ ( STR$ (B),1)
960  FOR I = 1 TO  LEN (F$(S))
965 C$ =  MID$ (F$(S),I,1)
970  IF C$ = B$ THEN 990
975  NEXT I
980  GOTO 995
990 L$ = "": IF I - 1 > 0 THEN L$ =  LEFT$ (F$(S),I - 1)
991 R$ = "": IF ( LEN (F$(S)) - I) > 0 THEN R$ =  RIGHT$ (F$(S), LEN (F$(S)) - I)
992 F$(S) = L$ + R$: IF  LEN (F$(S)) = 0 THEN F$(S) = BK$(S)
995  IF SK = 2 THEN  RETURN 
996 SK = 2: GOTO 955
999  REM   STATES
1000  DATA   A,-,-,-,-,-
1010  DATA   A,-,3,D,-,-
1020  DATA   A,-,-,D,5,-
1030  DATA   A,2,C,-,-,-
1040  DATA   A,B,3,-,-,-
1050  DATA   A,-,C,-,5,-
1060  DATA   A,2,-,D,-,-
1070  DATA   A,B,-,4,-,-
1080  DATA   A,B,-,-,5,-
1090  DATA   A,2,-,-,E,-
2000  DATA   A,-,C,4,-,-
2010  DATA   A,B,-,-,-,6
2020  DATA   A,2,-,-,-,F
2030  DATA   A,-,-,4,E,-
2040  DATA   A,-,C,-,-,6
2050  DATA   A,-,3,-,-,F
2060  DATA   A,-,-,4,-,F
2070  DATA   A,-,-,D,-,6
2080  DATA   A,-,3,-,E,-
2090  DATA   A,-,-,-,5,F
3000  DATA   A,-,-,-,E,6
3001  REM   WINNINGS GAME1
3100  DATA   A,2,C,4,E,-
3110  DATA   A,2,C,-,E,6
3120  DATA   A,-,C,4,E,6
3199  REM   WINNINGS GAME2
3200  DATA   A,B,C,4,5,-
3210  DATA   A,B,C,4,-,6
3220  DATA   A,B,C,-,5,6
3230  DATA   A,B,3,4,-,F
3240  DATA   A,B,3,-,5,F
3250  DATA   A,B,-,4,5,F
3260  DATA   A,2,3,-,E,F
3270  DATA   A,2,-,4,E,F
3280  DATA   A,-,3,4,E,F
3299  REM   WINNINGS GAME3
3300  DATA   A,B,3,D,5,-
3310  DATA   A,B,3,D,-,6
3320  DATA   A,B,-,D,5,6
3330  DATA   A,B,3,4,E,-
3340  DATA   A,B,3,-,E,6
3350  DATA   A,B,-,4,E,6
3360  DATA   A,2,C,4,-,F
3370  DATA   A,2,C,-,5,F
3380  DATA   A,-,C,4,5,F
3390  DATA   A,2,3,D,-,F
3400  DATA   A,2,-,D,5,F
3410  DATA   A,-,3,D,5,F
3420  DATA   A,2,C,D,5,-
3430  DATA   A,2,C,D,-,6
3440  DATA   A,-,C,D,5,6
3450  DATA   A,2,3,D,E,-
3460  DATA   A,2,-,D,E,6
3470  DATA   A,-,3,D,E,6
5000  CLS 
5010  PRINT "      A"
5020  PRINT "      " +  MID$ (P$, ASC ( MID$ (SC$,1,1)),1)
5030  PRINT "     / \            @=PCM"
5040  PRINT "  F /   \ B         *=";N$
5050  PRINT S$ +  MID$ (P$, ASC ( MID$ (SC$,6,1)),1) + T$ +  MID$ (P$, ASC ( MID$ (SC$,2,1)),1)
5060  PRINT "   !     !"
5070  PRINT "   !     !"
5080  PRINT S$ +  MID$ (P$, ASC ( MID$ (SC$,5,1)),1) + T$ +  MID$ (P$, ASC ( MID$ (SC$,3,1)),1)
5090  PRINT "  E \   / C"
5100  PRINT "     \ /"
5110  PRINT "      " +  MID$ (P$, ASC ( MID$ (SC$,4,1)),1)
5120  PRINT "      D"
5200  RETURN 
7000  CLS 
7010  PRINT "       THE PAPER CUP MACHINE (PCM)"
7020  PRINT "      DEVISED BY PROF. FERRI OF THE"
7030  PRINT "      UNIVERSITY OF BOLOGNA (BY JM)"
7040  PRINT : PRINT "PURPOSE OF THIS GAME IS TO UNDERSTAND"
7050  PRINT "HOW AI SELF-LEARNING WORKS."
7060  PRINT "THREE GAMES CAN BE PLAYED:"
7070  PRINT : PRINT "- GAME 1"
7080  PRINT "YOU MUST PLACE THREE TOKENS IN"
7090  PRINT "NON-ADJACENT POSITIONS."
7100  PRINT "- GAME 2"
7110  PRINT "YOU MUST PLACE THREE TOKENS IN"
7120  PRINT "ADJACENT POSITIONS."
7130  PRINT "- GAME 3"
7140  PRINT "YOU MUST PLACE TWO TOKENS IN"
7150  PRINT "ADJACENT POSITIONS AND THE THIRD""
7160  PRINT "NON-ADJACENT TO THE FIRST TWO."
7170  PRINT : PRINT "YOUR FIRST MOVE ALWAYS STARTS FROM 'A'."
7180  PRINT "PCM WILL IMPROVE AFTER EVERY MATCH TO"
7190  PRINT "THE POINT OF DEFEATING YOU EVERY TIME."
7195  PRINT "---PRESS ENTER TO CONTINUE"
7200  INPUT "";Z$: CLS : RETURN 

