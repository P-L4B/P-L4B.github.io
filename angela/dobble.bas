1  REM  DOBBLE - 2024 BY JADER MONARI - MASSIMO FERRI
2  REM  WRITTEN FOR APPLE-1'S APPLESOFT BASIC BY CLAUDIO PARMIGIANI - P-LAB
10  DIM M(456): DIM A1(8): DIM B1(8): DIM H1(8)
20  GOSUB 1000: REM  INTRO
30  GOSUB 2200
40  GOSUB 2300
50  GOSUB 2400
60  GOSUB 2500
70  GOSUB 2600
80  GOSUB 2800
999  END 
1000  REM  GENERATE CARDS AND SYMBOLS
1020 BS = 1:N = 8:F = 1:C = 2
1030 NC = N + (N - 1) * (N - 1):PT = 1
1040  PRINT "PLEASE WAIT, GENERATING CARDS";
1050  FOR I = 1 TO N: REM  GENERATE FIRST N*N MATRIX
1060  PRINT ".";
1070 M(PT) = F:PT = PT + 1
1080  FOR J = 1 TO (N - 1)
1090 M(PT) = C:PT = PT + 1:C = C + 1
1100  NEXT J,I
1120  FOR K = 2 TO N: REM  GENERATE SERIES FROM N+1 TO N+(N-1)*(N-1)
1130  PRINT ".";
1140  FOR I = 0 TO (N - 2)
1150 M(PT) = K:PT = PT + 1
1160  FOR J = 0 TO (N - 2)
1170 R = J:C = (I + (K - 2) * J) - (N - 1) *  INT ((I + (K - 2) * J) / (N - 1))
1180 EL = M(BS + (R + 1) * 8 + (C + 1))
1190 M(PT) = EL:PT = PT + 1
1200  NEXT J,I,K
1210  PRINT 
1220  RETURN 
2000  REM 
2010  PRINT "HOW TO PLAY:"
2020  PRINT : PRINT "THERE IS ONLY ONE TWO-DIGIT NUMBER IN"
2030  PRINT "COMMON BETWEEN ANY TWO CARD."
2040  PRINT "BE QUICK TO IDENTIFY THE ONE IN COMMON"
2050  PRINT "BETWEEN YOUR CARD AND THE DECK."
2060  PRINT "PRESS THE CORRESPONDING LETTER BEFORE"
2070  PRINT "YOUR OPPONENT!"
2080  PRINT : PRINT "TWO POSSIBLE GAMES:"
2090  PRINT : PRINT "- THE TOWERING INFERNO: ACCUMULATE MORE"
2100  PRINT "  CARDS THAN YOUR OPPONENT."
2120  PRINT : PRINT "- THE WELL: DISPOSE OF ALL THE CARDS"
2130  PRINT "  INITIALLY IN YOUR POSSESSION."
2140  PRINT : PRINT "ON SCREEN COUNTERS WILL INDICATE THE"
2150  PRINT "GAME PROGRESS.": PRINT 
2160  PRINT : PRINT : PRINT "PRESS ENTER TO CONTINUE"
2170  INPUT G$
2199  GOTO 2220
2200  PRINT : PRINT : PRINT : PRINT "              D O B B L E": PRINT 
2205  PRINT : PRINT "   C64 VERSION BY JADER MONARI (2024)"
2206  PRINT "CARD GENERATION ALGORITHM: MASSIMO FERRI"
2210  PRINT "   CODED IN APPLESOFT BASIC BY P-LAB"
2220  PRINT : PRINT "SELECT THE GAME:": PRINT 
2230  PRINT "(T) FOR THE TOWERING INFERNO"
2240  PRINT "(W) FOR THE WELL"
2245  PRINT : PRINT "(I) FOR INSTRUCTIONS"
2250  FOR I = 1 TO 5: PRINT : NEXT I:D = 17
2251  PRINT "PRESS REPEATEDLY TO SELECT GAME AND SET": PRINT "THE RANDOM NUMBER GENERATOR"
2252  IF  PEEK (53265) < 147 THEN A =  RND (10): GOTO 2252: REM  APPLE-1
2253 G$ =  CHR$ ( PEEK (53264) - 128): REM  APPLE-1
2254  REM  GET G$:IF G$=""THEN 2254: REM APPLE-2
2255  IF G$ = "I" THEN 2000
2260  IF G$ = "W" THEN A = 26:B = 26:O = 0: GOTO 2280
2270 A = 0:B = 0:O = 52
2280  INPUT "PLAYER A: YOUR NAME ";A$
2282  INPUT "PLAYER B: YOUR NAME ";B$
2290  RETURN 
2300  PRINT "PLEASE WAIT"
2305  PRINT "SHUFFLING CARD SYMBOLS...";
2310  FOR C = 0 TO 455 STEP 8
2315  PRINT ".";
2320  FOR S = 1 TO 8
2330 R =  INT ( RND (10) * 6) + 1: REM  RANDOM GEN FOR APPLE-1
2331  REM  R=INT(RND(-1*(PEEK(78)+256*PEEK(79)))*6)+1: REM RANDOM GEN FOR APPLE-2
2340 T = M(S + C):M(S + C) = M(R + C):M(R + C) = T
2350  NEXT S
2360  NEXT C
2370  PRINT " DONE.": RETURN 
2400  PRINT "SHUFFLING DECK...";
2410  FOR I = 0 TO 56
2420 X =  INT ( RND (10) * 57): REM  RANDOM GEN FOR APPLE-1
2421  REM  X=INT(RND(-1*(PEEK(78)+256*PEEK(79)))*57): REM RANDOM GEN FOR APPLE-2
2430 C = I * 8 + 1:R = X * 8 + 1
2440  FOR S = 0 TO 7
2450 T = M(C + S):M(C + S) = M(R + S):M(R + S) = T
2460  NEXT S
2465  PRINT ".";
2470  NEXT I
2480  PRINT " DONE.": RETURN 
2500  REM  INITIAL LOADING
2510  FOR C = 0 TO 16 STEP 8
2520  FOR S = 1 TO 8
2530  IF C = 0 THEN A1(S) = M(S + C)
2540  IF C = 8 THEN B1(S) = M(S + C)
2550  IF C = 16 THEN D1(S) = M(S + C)
2560  NEXT S: NEXT C
2570  RETURN 
2600  REM  PRINT CURRENT GAME
2610  PRINT : PRINT A$;":    CARDS:";A
2615  PRINT "------------------------"
2620  FOR I = 1 TO 8
2625  IF A1(I) < 10 THEN  PRINT "0";
2630  PRINT A1(I);" ";
2640  NEXT I
2645  PRINT : PRINT "A  B  C  D  E  F  G  H"
2647  PRINT "------------------------": PRINT 
2650  PRINT : PRINT B$;":    CARDS:";B:
2655  PRINT "------------------------"
2660  FOR I = 1 TO 8
2665  IF B1(I) < 10 THEN  PRINT "0";
2670  PRINT B1(I);" ";
2680  NEXT I
2685  PRINT : PRINT "I  J  K  L  M  N  O  P"
2687  PRINT "------------------------"
2690  PRINT : PRINT : PRINT "DECK:           CARDS:";O
2695  PRINT "------------------------"
2700  FOR I = 1 TO 8
2705  IF D1(I) < 10 THEN  PRINT "0";
2710  PRINT D1(I);" ";
2720  NEXT I
2725  PRINT : PRINT "------------------------"
2730  PRINT : PRINT : PRINT "SPOT-IT!"
2740  RETURN 
2800 W = 0:Y = 0
2810  GET X$: IF X$ = "" THEN 2810
2820 ID =  ASC (X$)
2830  IF ID < 73 THEN ID = ID - 64:P = 1: GOTO 2850
2840 ID = ID - 72:P = 2
2850  IF ID < 1 OR ID > 8 THEN 2810: REM  CHAR NOT ALLOWED
2870  IF P = 1 THEN  GOSUB 3000
2880  IF P = 2 THEN  GOSUB 3100
2890  IF W = 1 OR Y = 1 THEN  GOSUB 4000:W = 0:Y = 0: GOSUB 2600
2900  GOTO 2810
3000  FOR I = 1 TO 8
3010  IF A1(ID) = D1(I) THEN  PRINT : PRINT A$;" IS CORRECT: ";D1(I):W = 1: RETURN 
3020  NEXT I
3030  PRINT "X";
3040  RETURN 
3100  FOR I = 1 TO 8
3110  IF B1(ID) = D1(I) THEN  PRINT : PRINT B$;" IS CORRECT: ";D1(I):Y = 1: RETURN 
3120  NEXT I
3130  PRINT "X";
3140  RETURN 
4000  REM  CHECK WIN
4010  IF G$ = "W" AND W = 1 THEN A = A - 1:O = O + 1
4020  IF G$ = "W" AND Y = 1 THEN B = B - 1:O = O + 1
4030  IF G$ = "T" AND W = 1 THEN A = A + 1:O = O - 1
4040  IF G$ = "T" AND Y = 1 THEN B = B + 1:O = O - 1
4050  IF G$ = "W" AND A = 0 THEN  PRINT : PRINT A$;: GOTO 5000
4060  IF G$ = "W" AND B = 0 THEN  PRINT : PRINT B$;: GOTO 5000
4070  IF G$ = "T" AND O = 0 THEN 4500
4080  REM  NO WIN, UPDATE DECKS
4100  IF G$ = "W" THEN  GOSUB 6000
4110  IF G$ = "T" THEN  GOSUB 6100
4120  RETURN 
4500  IF A > B THEN  PRINT A$;: GOTO 5000
4510  PRINT B$;
5000  PRINT " WINS! CONGRATULATIONS!"
5010  PRINT : INPUT "ANOTHER GAME ?";G$
5020  IF G$ = "Y" THEN 30
5030  END 
6000  REM  WELL GAME: PLAYER CARD GOES TO TOP DECK
6010  FOR I = 1 TO 8
6020  IF W = 1 THEN D1(I) = A1(I)
6030  IF Y = 1 THEN D1(I) = B1(I)
6040  NEXT I
6050  REM  PICK PLAYER'S NEXT CARD
6060 D = D + 8:V = 1
6070  FOR I = D TO D + 7
6080  IF W = 1 THEN A1(V) = M(I)
6085  IF Y = 1 THEN B1(V) = M(I)
6087 V = V + 1
6090  NEXT I
6099  RETURN 
6100  REM  TOWER GAME: DECK CARD GOES TO PLAYER DECK: OK
6110  FOR I = 1 TO 8
6120  IF W = 1 THEN A1(I) = D1(I)
6130  IF Y = 1 THEN B1(I) = D1(I)
6140  NEXT I
6150  REM  PICK DECK'S NEXT CARD
6160 D = D + 8:V = 1
6170  FOR I = D TO D + 7
6180  IF W = 1 THEN D1(V) = M(I)
6185  IF Y = 1 THEN D1(V) = M(I)
6187 V = V + 1
6190  NEXT I
6199  RETURN 
