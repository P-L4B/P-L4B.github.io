LOMEM=640
5 POKE -6030,234: POKE -6029,234
10 DIM A1(8): DIM B1(8): DIM D1(8): DIM A$(8): DIM B$(8)
20 GOSUB 1000
30 GOSUB 2200
40 GOSUB 2300
50 GOSUB 2400
60 GOSUB 2500
70 GOSUB 2600
80 GOSUB 2800

1000 B1=1:N=8:F=1:C=2:P1=1:Q=871      
1040 PRINT "CREATING CARDS";
1050 FOR I=1 TO N: PRINT ".";: POKE (Q+P1),F:P1=P1+1
1080 FOR J=1 TO (N-1): POKE (Q+P1),C:P1=P1+1:C=C+1
1100 NEXT J,I
1120 FOR K=2 TO N: PRINT ".";
1140 FOR I=0 TO (N-2)
1150 POKE (Q+P1),K:P1=P1+1
1160 FOR J=0 TO (N-2)
1170 R=J:C=(I+(K-2)*J)-(N-1)*((I+(K-2)*J)/(N-1)):E1= PEEK (B1+(R+1)*8+(C+1)+Q): POKE (Q+P1),E1:P1=P1+1
1200 NEXT J,I,K: PRINT : RETURN      

2200 FOR I=1 TO 10: PRINT : NEXT I
2205 TAB (14): PRINT "D O B B L E" : PRINT : PRINT 
2210 PRINT "SELECT THE GAME:": PRINT : PRINT "(T) FOR THE TOWERING INFERNO": PRINT "(W) FOR THE WELL"
2250 FOR I=1 TO 7: PRINT : NEXT I:D=17
2251 IF PEEK (-12271)>=128 THEN 2260
2252 A= RND (256): GOTO 2251
2260 IF PEEK (-12272)#215 THEN 2270
2265 A=26:B=26:O=0:G$="W": GOTO 2275
2270 A=0:B=0:O=52:G$="T"
2275 PRINT : PRINT "MAX NAME LENGTH IS 8 CHARS"
2280 INPUT "PLAYER A",A$
2282 INPUT "PLAYER B",B$
2290 RETURN 

2300 PRINT "SHUFFLING";
2310 FOR C=0 TO 455 STEP 8: PRINT ".";
2320 FOR S=1 TO 8:R= RND (7)+1:T= PEEK (Q+S+C): POKE (Q+S+C), PEEK (Q+R+C): POKE (Q+R+C),T
2350 NEXT S,C: RETURN 

2400 FOR I=0 TO 56
2420 X= RND (57):C=I*8+1:R=X*8+1
2440 FOR S=0 TO 7
2450 T= PEEK (Q+C+S): POKE (Q+C+S), PEEK (Q+R+S): POKE (Q+R+S),T
2460 NEXT S
2465 PRINT ".";: NEXT I: PRINT " DONE.": RETURN 

2500 FOR C=0 TO 16 STEP 8
2520 FOR S=1 TO 8
2530 IF C=0 THEN A1(S)= PEEK (Q+S+C)
2540 IF C=8 THEN B1(S)= PEEK (Q+S+C)
2550 IF C=16 THEN D1(S)= PEEK (Q+S+C)
2560 NEXT S,C: RETURN 

2600 PRINT : PRINT A$;":    CARDS:";A
2615 GOSUB 2750
2620 FOR I=1 TO 8
2625 IF A1(I)<10 THEN PRINT "0";
2630 PRINT A1(I);" ";: NEXT I
2645 PRINT : PRINT "A  B  C  D  E  F  G  H": GOSUB 2750
2650 PRINT : PRINT B$;":    CARDS:";B: GOSUB 2750
2660 FOR I=1 TO 8
2665 IF B1(I)<10 THEN PRINT "0";
2670 PRINT B1(I);" ";: NEXT I
2685 PRINT : PRINT "I  J  K  L  M  N  O  P": GOSUB 2750
2690 PRINT : PRINT : PRINT "DECK:           CARDS:";O: GOSUB 2750      
2700 FOR I=1 TO 8
2705 IF D1(I)<10 THEN PRINT "0";
2710 PRINT D1(I);" ";: NEXT I: PRINT : GOSUB 2750: PRINT : PRINT : PRINT "SPOT-IT!": RETURN       

2750 PRINT "------------------------": RETURN 

2800 W=0:Y=0
2810 IF PEEK (-12271)<147 THEN 2810
2820 X= PEEK (-12272)
2830 IF X<193 OR X>208 OR X=U THEN 2810
2840 IF X>192 THEN P=1
2845 IF X>200 THEN P=2
2850 IF X<=208 THEN I1=X-200
2855 IF X<=200 THEN I1=X-192
2857 U=X
2860 IF P=1 THEN GOSUB 3000
2870 IF P=2 THEN GOSUB 3100
2880 IF W=1 OR Y=1 THEN GOSUB 4000      
2890 GOTO 2810

3000 FOR I=1 TO 8
3010 IF A1(I1)<>D1(I) THEN 3020      
3015 PRINT : PRINT A$;" IS CORRECT: ";D1(I):W=1: GOSUB 3500: RETURN       
3020 NEXT I: PRINT "X";: RETURN 
      
3100 FOR I=1 TO 8
3110 IF B1(I1)<>D1(I) THEN 3120      
3115 PRINT : PRINT B$;" IS CORRECT: ";D1(I):Y=1: GOSUB 3500: RETURN       
3120 NEXT I: PRINT "X";: RETURN       

3500 POKE 251,( PEEK (251)-1): RETURN       

4000 IF G$="W" AND W=1 THEN GOSUB 4200
4020 IF G$="W" AND Y=1 THEN GOSUB 4201
4030 IF G$="T" AND W=1 THEN GOSUB 4202
4040 IF G$="T" AND Y=1 THEN GOSUB 4203
4050 IF G$="W" AND A=0 THEN GOSUB 4204
4060 IF G$="W" AND B=0 THEN GOSUB 4205
4070 IF G$="T" AND O=0 THEN 4500      
4100 IF G$="W" THEN GOSUB 6000
4110 IF G$="T" THEN GOSUB 6100
4120 W=0:Y=0: GOSUB 2600: RETURN 
      
4200 A=A-1:O=O+1: RETURN 
4201 B=B-1:O=O+1: RETURN 
4202 A=A+1:O=O-1: RETURN 
4203 B=B+1:O=O-1: RETURN 
4204 PRINT : PRINT A$;: GOTO 5000: RETURN 
4205 PRINT : PRINT B$;: GOTO 5000: RETURN 

4500 IF A<B THEN 4510
4505 PRINT A$;: GOTO 5000
4510 PRINT B$;
5000 PRINT " WINS! CONGRATULATIONS!"      
5010 PRINT : INPUT "AGAIN ",G$
5020 IF G$="Y" THEN 30
5030 END 

6000 FOR I=1 TO 8
6020 IF W=1 THEN D1(I)=A1(I)
6030 IF Y=1 THEN D1(I)=B1(I)
6040 NEXT I
6060 D=D+8:V=1
6070 FOR I=D TO D+7
6080 IF W=1 THEN A1(V)= PEEK (Q+I)
6090 IF Y=1 THEN B1(V)= PEEK (Q+I)
6095 V=V+1: NEXT I: RETURN 

6100 FOR I=1 TO 8
6120 IF W=1 THEN A1(I)=D1(I)
6130 IF Y=1 THEN B1(I)=D1(I)
6140 NEXT I
6160 D=D+8:V=1
6170 FOR I=D TO D+7
6180 IF W=1 THEN D1(V)= PEEK (Q+I)
6185 IF Y=1 THEN D1(V)= PEEK (Q+I)
6190 V=V+1: NEXT I: RETURN 
